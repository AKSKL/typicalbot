plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'com.github.hierynomus.license' version '0.14.0'
}

group 'com.typicalbot'
version '3.0.1-alpha'

mainClassName = 'com.typicalbot.TypicalBot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // JDA
    compile group: 'net.dv8tion', name: 'JDA', version: '3.8.0_427'
    compile group: 'com.sedmelluq', name: 'jda-nas', version: '1.0.6'
    compile group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.7'

    // Database
    compile group: 'com.zaxxer', name: 'HikariCP', version: '3.2.0'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.47'

    // Logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    // Sentry
    compile group: 'io.sentry', name: 'sentry', version: '1.7.10'

    // Other
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'
    compile group: 'io.github.lukehutch', name: 'fast-classpath-scanner', version: '3.1.15'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.11.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava {
    classpath = sourceSets.main.compileClasspath
    options.encoding = 'UTF-8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

license {
    header = file('LICENSEHEADER.txt')
}

artifacts {
    archives shadowJar
}

jar {
    classifier = "nodeps"
    baseName 'TypicalBot'
}

shadowJar {
    classifier = ""
}

jacocoTestReport {
    reports {
        xml.setEnabled(true)
        html.setEnabled(false)
    }
}

check.dependsOn jacocoTestReport
build.dependsOn shadowJar